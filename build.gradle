plugins {
	id 'org.springframework.boot' version '2.2.2.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
	id 'java'
	id 'war'
}

group = 'com.sweagle'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

sourceSets {
	unitTest {
		compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        java.srcDirs file('src/test/java/com/sweagle/messageserver/unittest')
        
    }

    integrationTest {
		compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        java.srcDirs file('src/test/java/com/sweagle/messageserver/integrationtest')
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntime.extendsFrom runtimeOnly
    unitTestImplementation.extendsFrom implementation
    unitTestRuntime.extendsFrom runtimeOnly
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	implementation 'io.springfox:springfox-swagger2:2.9.2'
   	implementation 'io.springfox:springfox-swagger-ui:2.9.2'
   	integrationTestCompile 'org.springframework.boot:spring-boot-starter-test'
   	unitTestCompile 'org.springframework.boot:spring-boot-starter-test'
}

test {
	useJUnitPlatform()
	
	testLogging {
		events "passed", "skipped", "failed"
	}
}

task unitTest(type: Test) {
    description = 'Runs unit tests.'
    group = 'verification'

    testClassesDirs = sourceSets.unitTest.output.classesDirs
    classpath = sourceSets.unitTest.runtimeClasspath
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    
    shouldRunAfter unitTest
}



check.dependsOn integrationTest
check.dependsOn unitTest


