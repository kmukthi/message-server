plugins {
	id 'org.springframework.boot' version '2.2.2.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
	id 'java'
	id 'war'
}

group = 'com.sweagle'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

sourceSets {
    intTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    intTestImplementation.extendsFrom implementation
    intTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	implementation 'io.springfox:springfox-swagger2:2.9.2'
   	implementation 'io.springfox:springfox-swagger-ui:2.9.2'
   	intTestImplementation 'org.springframework.boot:spring-boot-starter-test'
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    
    testLogging {
		events "passed", "skipped", "failed"
	}

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    shouldRunAfter test
}

check.dependsOn integrationTest

test {
	useJUnitPlatform()
	
	testLogging {
		events "passed", "skipped", "failed"
	}
}

